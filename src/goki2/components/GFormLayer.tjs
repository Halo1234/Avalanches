/*
 $Author$
 $Revision$
*/


/**
 * 異なる GFormLayer 同士で softFocus のフォーカス移動を行いたい場合は
 * GFormLayer#focusable に false を設定して GWindow#softFocus に追加する方法があります。
 * 下記の例では GWindow#focusedLayer が null の時に矢印キーを押すと
 * formA と formB の softFocus が順番にフォーカスされます。
 *
 * 例：
 *  var formA = new GFormLayer(window, window.primaryLayer);
 *  var formB = new GFormLayer(window, window.primaryLayer);
 *
 *  formA.focusable = formB.focusable = false;
 *
 *  window.softFocus.add(formA.softFocus);
 *  window.softFocus.add(formB.softFocus);
**/
class GFormLayer extends GWidgetLayer
{
	var softFocus_;

	var showHideType_ = '';
	var showHideTime = 200;
	var showHideMover_;

	var clickCancel = false;				// click() で show()/hide() をキャンセルするかどうか
	var rclickHide = true;					// rclick() で hide() を呼ぶかどうか
	var softFocusDisableInShowHideRunning = true;
	var softFocusEnabledBackup_ = true;

	function GFormLayer(window, parent, handler)
	{
		super.GWidgetLayer(...);
		focusable = true;
		bodyColor = clAppWorkSpace;
		captionColor = clHighlightText;
		showHideType = 'vista';
	}

	function finalize()
	{
		// NOTE:
		// softFocus_ は family に入れると無効化するタイミングが不定になるので
		// ここで明示的に無効化します。
		invalidate softFocus_ if softFocus_ !== void;

		super.finalize(...);
	}

	function createFocus(name = void)
	{
		var obj = new GFocus(...);

		obj.visible = visible;
		obj.enabled = enabled;

		return obj;
	}

	/*
	 * \brief show()/hide() による表示/非表示効果のタイプを指定する事ができます。
	 *
	 * \remarks 利用可能な効果は下記のとおりです。
	 * \remarks
	 * \remarks      '': 効果を行いません。
	 * \remarks  'fade': 通常のフェードイン/フェードアウト効果です。
	 * \remarks 'vista': Windows Vista/7 の Aero のような効果です。
	 *
	 * これらは必要に応じて派生先で拡張するとよいでしょう。
	**/
	function setShowHideType(type)
	{
		showHideType_ = type;

		switch(type)
		{
		case '':
			break;

		case 'fade':
			with(showHideMover)
			{
				.useTransform = false;
				.withChildrens = false;
				.looping = false;
			}
			break;

		case 'vista':
			with(showHideMover)
			{
				.useTransform = true;
				.withChildrens = true;
				.looping = false;
			}
			break;

		default:
			onUnknownShowHideMethodType(type, showHideMover);
			break;
		}
	}

	function click(x, y, shift)
	{
		if(clickCancel && (showRunning || hideRunning))
			cancelShowHide();
	}

	function rclick(x, y, shift)
	{
		if(showRunning || hideRunning)
			click(...);
		else
		if(rclickHide)
			hide();
	}

	function beginShowEffect()
	{
		switch(showHideType_)
		{
		case '':
			super.beginShowEffect(...);
			break;

		case 'fade':
			with(showHideMover)
			{
				.clear();
				.addFrame(0.0, %[opacity : 0]);
				.addFrame(1.0, %[opacity : 255]);
				.time = showHideTime;
				.delay = 0;
				.accel = 0;
				.start();
			}
			break;

		case 'vista':
			with(showHideMover)
			{
				.clear();
				.addFrame(0.0, %[scale : 0.95, opacity : 0]);
				.addFrame(1.0, %[scale : 1.0, opacity : 255]);
				.time = showHideTime;
				.delay = 0;
				.accel = 1.8;
				.start();
			}
			break;

		default:
			onUnknownShowMethod(showHideType_, showHideMover);
			break;
		}
	}

	function beginHideEffect()
	{
		switch(showHideType_)
		{
		case '':
			super.beginHideEffect(...);
			break;

		case 'fade':
			with(showHideMover)
			{
				.clear();
				.addFrame(0.0, %[opacity : 255]);
				.addFrame(1.0, %[opacity : 0]);
				.delay = 0;
				.accel = 0;
				.start();
			}
			break;

		case 'vista':
			with(showHideMover)
			{
				.clear();
				.addFrame(0.0, %[scale : 1.0, opacity : 255]);
				.addFrame(1.0, %[scale : 0.95, opacity : 0]);
				.delay = 0;
				.accel = -1.8;
				.start();
			}
			break;

		default:
			onUnknownHideMethod(showHideType_, showHideMover);
			break;
		}
	}

	function onUnknownShowHideMethodType(type, mover)
	{
		throw new GArgumentsError(@"'&type;' は未定義の表示／非表示タイプです。");
	}

	function onUnknownShowMethod(type, mover)
	{
		throw new GArgumentsError(@"レイヤーを表示しようとしましたが '&type;' は未定義の効果タイプです。");
	}

	function onUnknownHideMethod(type, mover)
	{
		throw new GArgumentsError(@"レイヤーを非表示にしようとしましたが '&type;' は未定義の効果タイプです。");
	}

	/**
	 * フォーカスが変更された時に呼ばれます。
	 * focused が false なら何もフォーカスされていません。
	 * それ以外は何かがフォーカスされています。
	**/
	function onFocusChanged(focused)
	{
	}

	function onKeyDown(key, shift)
	{
		if(shift & ssShift)
		{
			switch(key)
			{
			case VK_LEFT:	key = VK_RIGHT; break;
			case VK_UP:		key = VK_DOWN; break;
			case VK_RIGHT:	key = VK_LEFT; break;
			case VK_DOWN:	key = VK_UP; break;
			}
		}

		switch(key)
		{
		case VK_LEFT:	onFocusChanged(softFocus.moveLeft(true)); break;
		case VK_UP:		onFocusChanged(softFocus.moveUp(true)); break;
		case VK_RIGHT:	onFocusChanged(softFocus.moveRight(true)); break;
		case VK_DOWN:	onFocusChanged(softFocus.moveDown(true)); break;
		case VK_RETURN:	softFocus.click() if !(shift & ssRepeat); break;
		case VK_SPACE:	rclick() if !(shift & ssRepeat); break;
		default:		return false;
		}

		return true;
	}

	function onMouseMove()
	{
		softFocus.kill() if softFocus_ !== void;
	}

	function onInterruptShowHide(drawFinalFrame)
	{
		if(showHideType_ != '')
		{
			if(drawFinalFrame)
				showHideMover.stop();
			else
				showHideMover.pause();
		}
	}

	function onStartTimeLine(event)
	{
		if(event === showHideMover)
		{
			focus();

			if(softFocusDisableInShowHideRunning && softFocus_ !== void)
			{
				softFocusEnabledBackup_ = softFocus.enabled;
				softFocus.enabled = false;
			}
		}
	}

	function onStopTimeLine(event, paused)
	{
		if(event === showHideMover)
		{
			if(paused)
			{
				stopShowHideOperation(false);
				return;
			}
			else
			if(showRunning)
			{
				if(softFocusDisableInShowHideRunning && softFocus_ !== void)
					softFocus.enabled = softFocusEnabledBackup_;
				showComplete();
			}
			else
			if(hideRunning)
			{
				if(softFocusDisableInShowHideRunning && softFocus_ !== void)
					softFocus.enabled = softFocusEnabledBackup_;
				hideComplete();
			}
		}
	}

	property softFocus
	{
		getter		{ return softFocus_ === void ? (softFocus_ = createFocus()) : softFocus_; }
	}

	property showHideType
	{
		setter(x)	{ setShowHideType(x); }
		getter		{ return showHideType_; }
	}

	property showHideMover
	{
		getter		{ return showHideMover_ === void ? family.receive(showHideMover_ = createMover(this)) : showHideMover_; }
	}

	property visible
	{
		setter(x)	{ super.visible = x; softFocus_.visible = x if softFocus_ !== void; }
		getter		{ return super.visible; }
	}

	property enabled
	{
		setter(x)	{ super.enabled = x; softFocus_.enabled = x if softFocus_ !== void; }
		getter		{ return super.enabled; }
	}
}



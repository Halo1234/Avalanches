/*
 * $Author$
 * $Revision$
**/


/**/
class ModRightClick extends GModule
{
	var rightClickEnabled = true;
	var rightClickJump = false;
	var rightClickCall = false;
	var rightClickStorage = void;
	var rightClickTarget = void;

	var conductor_;

	function ModRightClick(container, name)
	{
		super.GModule(...);

		conductor_ = createConductor(window);
	}

	/**/
	function createConductor(window)
	{
		return new GConductor(window);
	}

	/**/
	function createExtensionTagHandlers()
	{
		return %[

			/* @right_click */
			'right_click' => function (elm) {
				rightClickEnabled = GObjectUtils.getBoolean(elm, 'enabled') if GObjectUtils.hasValue(elm, 'enabled');
				if(GObjectUtils.hasValue(elm, 'jump'))
				{
					rightClickJump = GObjectUtils.getBoolean(elm, 'jump');
					if(rightClickJump)
						rightClickCall = false;
				}
				if(GObjectUtils.hasValue(elm, 'call'))
				{
					rightClickCall = GObjectUtils.getBoolean(elm, 'call');
					if(rightClickCall)
						rightClickJump = false;
				}
				rightClickStorage = elm.storage if elm.storage !== void;
				rightClickTarget = elm.target if elm.target !== void;
			} incontextof this,

		];
	}

	/**/
	function callExtraConductor(storage, target)
	{
		GObjectUtils.assignDictionary(conductor_.macros, window.conductor.macros);
		window.exchangeConductor(conductor_);

		if(storage == void)
		{
			storage = window.mainConductor.curStorage;
		}

		window.conductor.callScenario(storage, target);
		window.conductor.resetInterrupt();
		window.conductor.start();
	}

	/**/
	function returnExtraConductor()
	{
		conductor_.stop();
		conductor_.interrupt();

		window.resetConductor();
	}

	/**/
	function onWindowRightClick(x, y, shift)
	{
		if(!rightClickEnabled)
			return;
		if(rightClickJump)
		{
			window.conductor.jumpScenario(rightClickStorage, rightClickTarget);
			window.conductor.start();
		}
		else
		if(rightClickCall && window.conductor != conductor_)
		{
			callExtraConductor(rightClickStorage, rightClickTarget);
		}
		else
		{
			var mod;

			if((mod = container.get('modMessage')) !== void)
				mod.switchMessageLayerHiddenByUser();
		}
	}

	/**/
	function onConductorReturn(elm)
	{
		if(window.conductor == conductor_)
		{
			if(conductor_.callStackDepth == 1)
			{
				returnExtraConductor();

				if(elm.storage !== void || elm.target !== void)
				{
					conductor_.jumpScenario(elm.storage, elm.target);
				}
				elm.ignore = true;
			}
		}
	}
}


